#!!PERL_PATH!
#
# soft-menu-change
# The SoftEnv "software menu make changes" program
#
# This is part of Msys, the MCS Systems Administration Toolkit
# For more information, see http://www.mcs.anl.gov/systems/software/ .
#
# Copyright (c) University of Chicago 1999.
# See the COPYRIGHT file in the distribution for details on the copyright.
#
# ===========================================================================
#
# Description:
#  soft-menu-change takes the information from soft-menu-select and
#  deletes and adds the correct keys from the environment.  Most of the
#  errors are taken care of in soft-menu-select.
#
# ===========================================================================
#
# Authors and ID String:
#
#   Alan Bailey, bailey@mcs.anl.gov
#
# $Id: soft-menu-change,v 1.8 2003/08/28 03:20:41 finley Exp $
#
# ===========================================================================
#
#  soft-menu-change [-t -d] csh|sh group selection
#
#   csh|sh           : the shell syntax to output
#   group            : the group you want to display
#   selection        : the selection from the menu
#   -d               : turn on debugging
#   -t               : use the test database
#   -u -h --help     : helpful stuff
#   -v --version     : version
#
# ===========================================================================
#
# Exit codes:
#   0  Successful completion.
#   1  Error
#
# ===========================================================================

use Getopt::Std;

use strict;

use vars qw($debug $arch %db $program %envvars $shell);
use vars qw($database $test_database $arch_id_program);
use vars qw($package_name $version %supported);
use vars qw($group $selection);


&set_globals;

&do_subtractions;

&do_additions;

exit(0);


# -----------
# Subroutines
# -----------

sub set_globals {

  # read the site defaults
  push(@INC, 'SOFTENV_ETC_DIR');
  require 'softenv.config.pl';

  ($program = $0) =~ s:.*/::;   # here's the real name
  $program = "softmenu";            # we just want 'softmenu'

  chomp ($arch = `$arch_id_program`);
  $arch = "default" if (! $supported{$arch});
  %db = &read_database($database);

  &parse_commandline;
  print "The arch is $arch\n" if $debug;

}


sub parse_commandline {
  # Parse the commandline
  # First checks for -- options, then runs getopt
  # and finds the - options.  After that, it gets the
  # actual parameters csh|sh, add|del, and keyword

  use vars qw($opt_d $opt_t $opt_h $opt_u $opt_v);

  my($arg, $key);
  my($count) = 0;
  my($exists) = "no";

  if ($ARGV[0] eq "--version") {
    &version;
    exit(0);
  }
  if ($ARGV[0] eq "--help") {
    &usage;
    exit(0);
  }

  getopt('');
  $database = $test_database if ($opt_t);
  $debug = 1 if ($opt_d);

  if (($opt_h) or ($opt_u)) {
    &usage();
    exit(0);
  }
  if ($opt_v) {
    &version();
    exit(0);
  }

  foreach $arg (@ARGV) {
     if ($arg eq 0) {
      exit(0);
    }
  }
  if ($#ARGV != 2) {
    print "echo \"$program: Bad command line arguments\" \; \n";
    &usage;
    exit(1);
  }

  if ($ARGV[0] eq "csh") {
    $shell = "csh";
  } elsif ($ARGV[0] eq "sh") {
    $shell = "sh";
  } else {
    print "echo \"$program: Bad command line arguments\" \; \n";
    &usage;
    exit(1);
  }
  print "The shell is $shell\n" if $debug;

  if ($ARGV[1]) {
    $group = $ARGV[1];
  } else {
    print "echo \"$program: Bad command line arguments\" \; \n";
    &usage;
    exit(1);
  }
  foreach $key (sort keys %{ $db{GROUP} }) {
    next if ($db{GROUP}{$key} !~ /$group/);
    next if (! $db{ENV}{$key}{$arch});
    $exists = "ok";
  }
  if ($exists eq "no") {
    print "$program: group $group does not exist on this architecture\n";
    &usage;
    exit(0);
  }
  print "The group is $group\n" if $debug;

  if (defined $ARGV[2]) {
    $selection = $ARGV[2];
  } else {
    print "echo \"$program: Bad command line arguments\" \; \n";
    &usage;
    exit(1);
  }
  $exists = "no";
  foreach $key (sort keys %{ $db{GROUP} }) {
    next if ($db{GROUP}{$key} !~ /$group/);
    next if (! $db{ENV}{$key}{$arch});
    $count++;
    if ($selection eq $count) {
      $exists = "ok";
    }
  }
  if ($exists eq "no") {
    print "echo \"$program: Unknown selection $selection\" \; \n";
    exit(0);
  }
  print "The selection is $selection\n" if $debug;

}

sub do_subtractions {
  my($key, $count);

  $count = 0;
  print "echo \"Removing previous $group versions from your environment...\" \;\n";
  foreach $key (sort keys %{ $db{GROUP} }) {
    next if ($db{GROUP}{$key} !~ /$group/);
    next if (! $db{ENV}{$key}{$arch});
    $count++;
    print "Removing $key\n" if $debug;
    %envvars = ();
    &find_vars_to_set($key);
    &output_subtraction($key);
  }
}

sub do_additions {
  my($key, $count);

  $count = 0;
  foreach $key (sort keys %{ $db{GROUP} }) {
    next if ($db{GROUP}{$key} !~ /$group/);
    next if (! $db{ENV}{$key}{$arch});
    $count++;
    next unless ($count eq $selection);
    print "Adding $key\n" if $debug;
    %envvars = ();
    &find_vars_to_set($key);
    print "echo \"Adding $key to your environment...\" \;\n";
    &output_addition($key);
  }
}


sub find_vars_to_set {
  # Looks in the %db hash to get the variables and values involved in
  # this operation.  Checks for econs in another routine.
  
  my($e, $found_econ, $var);
  my($keyword) = @_;

  if (! $db{"ENV"}{$keyword}{$arch}) {
    print "echo \"$program: No changes associated with keyword $keyword\" \; \n";
    return;
  }
 
  $found_econ = 0;

  foreach $e (split / /, $db{"ENV"}{$keyword}{$arch}{"ECONS"}{"ORDER"}) {
    if (&econ_is_true($e)) {
      print "Found the following variables:\n" if $debug;
      foreach $var (keys %{ $db{"ENV"}{$keyword}{$arch}{$e} }) {
        print "$var -> ", $db{"ENV"}{$keyword}{$arch}{$e}{$var}, "\n" if $debug;
        $envvars{$var} = $db{"ENV"}{$keyword}{$arch}{$e}{$var};
      }
      $found_econ = 1;
    }
  }
  if (! $found_econ) {
    print "Found the following variables:\n" if $debug;
    foreach $var (keys %{ $db{"ENV"}{$keyword}{$arch}{"NO-ECON"} }) {
      print "$var -> ", $db{"ENV"}{$keyword}{$arch}{"NO-ECON"}{$var}, "\n" if $debug;
      $envvars{$var} = $db{"ENV"}{$keyword}{$arch}{"NO-ECON"}{$var};
    }
  }

}


sub econ_is_true {

  my($econ, $lhs, $rhs) = @_;

  print "testing $econ...\n" if $debug;

  if ($econ =~ /^(.*)\!\=(.*)$/) {
    $lhs = $1;
    $rhs = $2;
    if ($ENV{$lhs} ne $rhs) {
      print "Yes, $lhs != $rhs\n" if $debug;
      return 1;
    } else {
      print "No, $lhs = $rhs\n" if $debug;
      return 0;
    }
  } elsif ($econ =~ /^(.*)\=(.*)$/) {
    $lhs = $1;
    $rhs = $2;
    if ($ENV{$lhs} eq $rhs) {
      print "Yes, $lhs = $rhs\n" if $debug;
      return 1;
    } else {
      print "No, $lhs != $rhs\n" if $debug;
      return 0;
    }
  }
}

sub output_addition {
  # Output shell code (either csh or sh) lines for the keyword
  # Basically a bunch of setenv lines, it checks for the accumulate
  # indicator, and also if the variable is already set to something

  my($keyword) = @_;
  my($var, @var_order);

  if ($db{"FLAGS"}{$keyword} =~ /ordered/) {
    @var_order = split(/ /, $envvars{"SOFT_VAR_ORDER"});
  } else {
    @var_order = sort keys %envvars;
  }

  foreach $var (@var_order) {
    next if ($var eq "SOFT_VAR_ORDER");
    if ($shell eq "csh") {
      if ($var =~ /^command(\d+)/) {
        print "$envvars{$var} \; \n";
      } elsif ($var =~ /^source(\d+)/) {
        print "source $envvars{$var}.csh \; \n";
      } elsif ($db{"ACCUMULATE"}{$var}) {
        if ($ENV{$var} eq "") {
          print "setenv $var $envvars{$var} \; \n";
        } else {
          print "setenv $var $envvars{$var}", 
                $db{"ACCUMULATE"}{$var}, "\$\{$var\} \;\n";
        }
      } else {
        print "setenv $var $envvars{$var} \; \n";
      }
    } else {
      if ($var =~ /^command(\d+)/) {
        print "$envvars{$var} \; \n";
      } elsif ($var =~ /^source(\d+)/) {
        print ". $envvars{$var}.sh \; \n";
      } elsif ($db{"ACCUMULATE"}{$var}) {
        if ($ENV{$var} eq "") {
          print "$var=$envvars{$var} \; \n";
        } else {
          print "$var=$envvars{$var}",
                $db{"ACCUMULATE"}{$var}, "\$\{$var\} \;\n";
        }
      } else {
        print "$var=$envvars{$var} \; \n";
      }
    }
  }
}


sub output_subtraction {
  # Output shell code (either csh or sh) lines for the keyword
  # Basically a bunch of unsetenv lines, it checks for the accumulate
  # indicator, and also if the variable is already set to something.
  # For unsetting PATH-like variables, a sed trick is used.  First the
  # variable is surrounded with :'s, to make everything equivalent, then
  # the value is found and deleted.

  my($keyword) = @_;
  my($var, @var_order);

  # Do the deletion in backwards order from the order specified
  if ($db{"FLAGS"}{$keyword} =~ /ordered/) {
    @var_order = reverse(split(/ /, $envvars{"SOFT_VAR_ORDER"}));
  } else {
    @var_order = sort keys %envvars;
  }

  foreach $var (@var_order) {
    next if ($var eq "SOFT_VAR_ORDER");
    if ($shell eq "csh") {
      if ($var =~ /^command(\d+)/) {
        # nothing, no way to remove this
      } elsif ($var =~ /^source(\d+)/) {
        # nothing, no way to remove this
      } elsif ($db{"ACCUMULATE"}{$var}) {
        if ($ENV{$var} eq "") {
          # print "setenv $var $envvars{$var} \; \n";
        } else {
          # print "setenv $var $envvars{$var}$accumulate{$var}\$\{$var\} \;\n";
          print "setenv SOFT_REMOVE $envvars{$var} \; \n";
          print "setenv $var \`echo \$\{$var\} | sed -e \'s+.\*+\:\&\:+g\' -e \"s#\\\$\{SOFT_REMOVE\}#@@@#g\" | sed -e \'s+\:@@@\[\:@@@\]\*\:+\:+g\' | sed -e \'s+\^\:++\' -e \'s+\:\$++\' -e \"s#@@@#\$\{SOFT_REMOVE\}#g\" \` \; \n";
          print "unsetenv SOFT_REMOVE \; \n";
        }
      } else {
        # print "setenv $var $envvars{$var} \; \n";
        print "unsetenv $var \; \n";
      }
    } else {
      if ($var =~ /^command(\d+)/) {
        # nothing, no way to remove this
      } elsif ($var =~ /^source(\d+)/) {
        # nothing, no way to remove this
      } elsif ($db{"ACCUMULATE"}{$var}) {
        if ($ENV{$var} eq "") { 
          # print "$var=$envvars{$var} \; \n";
        } else {
          # print "$var=$envvars{$var}$accumulate{$var}\$\{$var\} \;\n";
          print "SOFT_REMOVE=$envvars{$var} \; \n";
          print "$var=\`echo \$\{$var\} | sed -e \'s+.\*+\:\&\:+g\' -e \"s#\\\$\{SOFT_REMOVE\}#@@@#g\" | sed -e \'s+\:@@@\[\:@@@\]\*\:+\:+g\' | sed -e \'s+\^\:++\' -e \'s+\:\$++\' -e \"s#@@@#\$\{SOFT_REMOVE\}#g\" \` \; \n";
          print "unset SOFT_REMOVE \; \n";
        }
      } else {
        # print "$var=$envvars{$var} \; \n";
        print "unset $var \; \n";
      }
    }
  }
}


sub version {
  # Version
print <<"EOF";

$program is part of $package_name version $version

EOF
}

sub usage {
  # Usage
print <<"EOF";
echo "";
echo "$program is part of $package_name version $version ";
echo "";
echo "  Usage:";
echo "    $program group";
echo "     group        : group from which to choose";
echo "";
echo "";
echo "  Purpose:";
echo "     $program is part of the $package_name system.  It shows a menu";
echo "     and lets you choose a key to dynamically add.";
EOF
}

sub read_database {
  # Read the software database, put it in a hash, return the hash.
  # Instead of dropping P, M, E directly in, parse them into subhashes.
  #
  # Original routine in soft-msc
  #
  my($file) = @_;
  my($x, $key, $val, %db);   
     
     
  open (DB, $file) || die "$program: Unable to open $file\n";
  while(<DB>) {
    $x = chomp;
    if(! $x eq "\n") { $_ .= $x; }
    ($key,$val)=split(/->/, $_, 2);
   
    if($key =~ s/^MACRO\://) {
      $db{MACRO}{$key}=$val;
    }
    if($key =~ s/^MACRO_ARCHES\://) {
      $db{MACRO_ARCHES}{$key}=$val;
    }
    if($key =~ s/^DESC\://) {
      $db{DESC}{$key}=$val;
    }
    if($key =~ s/^ACCUMULATE\://) {
      $db{ACCUMULATE}{$key}=$val;
    }
    if($key =~ s/^UNIQUE\://) {
      $db{UNIQUE}{$key}=1;
    }
    if($key =~ s/^FLAGS\://) {
      $db{FLAGS}{$key}=$val;
    }
    if($key =~ s/^GROUP\://) {
      $db{GROUP}{$key}=$val;
    }
    if($key =~ /^INITIALIZE\:(.+):(\d+\:(.+)$)/) {
      $db{INITIALIZE}{$1}{$2}=$val;
    }
    if($key =~ /^ENV:(.+):(.+):(.+):(.+)$/) {
      $db{ENV}{$1}{$2}{$3}{$4}=&bracify($val);
    }
  }  
  close DB;
  %db;
}



sub bracify {
  # Take some string, like /home/foo/$ARCH and change it into
  # /home/foo/${ARCH}.  This is done because, otherwise, shells can
  # get fairly picky about what you append to such a string.
  #
  # We assume that only letters, numbers, and _ constitute variable
  # names.
  #
  # Original routine in soft-msc
  #
  my($s) = @_;

  while ($s =~ /\$[A-Za-z0-9_]+/) {
    $s =~ s/\$([A-Za-z0-9_]+)/\$\{$1\}/;
  }
  $s;
}

